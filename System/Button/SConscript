#-----------------------------------------------------------------------------
# Brio/System/Button
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *
import os

Import('env')

module_name	= 'Button'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
if env['cpu'] == 'x86':
	emulibs		= ['Emulation']
	modsrcs		= Glob('*.cpp') + Glob( os.path.join('Emulation', '*.cpp') )

else:
	emulibs		= []
	modsrcs		= Glob('*.cpp') + Glob( os.path.join( env['platform'], '*.cpp') )

mpisrcs		= Glob(os.path.join('PublicMPI', '*.cpp')) + modsrcs
modlibs		= ['DebugMPI', 'EventMPI', 'KernelMPI'] + emulibs
mpilibs		= ['Utility']
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module
#-----------------------------------------------------------------------------
myenv		= env.Clone()
myenv.Append(CPPPATH=['Include'])
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI)

targets = mympi or []

#-----------------------------------------------------------------------------
# Modify the build environment and build and run the unit tests
#-----------------------------------------------------------------------------
if env['buildtests']:
	targets.append(RunMyTests(module_name, [], mpilibs + ['EventMPI'] + emulibs, myenv))

Return('targets')
