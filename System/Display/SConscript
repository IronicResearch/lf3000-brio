#-----------------------------------------------------------------------------
# Brio/System/Display
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *
Import('*')

module_name	= 'Display'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
emulibs		= vars['is_emulation'] and ['X11'] or []
hwlibs		= vars['variant'] == 'LF1000' and ['opengles_lite'] or ['ogl']
ogllibs		= vars['is_emulation'] and ['gles_cl'] or hwlibs
modsrcs		= FindModuleSources(Dir('.'), vars)
mpisrcs		= FindMPISources(Dir('.'), vars)
modlibs		= ['DebugMPI', 'EventMPI', 'KernelMPI'] + emulibs
mpilibs		= ['ModuleMPI'] + ogllibs					
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module
#-----------------------------------------------------------------------------
myenv		= env.Copy()
if vars['is_emulation']:
	myenv.Append(CPPPATH = ['#ThirdParty/PowerVR/Include',
							'#ThirdParty/PowerVR/Include/LinuxPC',
							'/usr/X11R6/include'])
	myenv.Append(LIBPATH = ['#ThirdParty/PowerVR/Libs'])
	myenv.Append(RPATH   = myenv.Dir('#ThirdParty/PowerVR/Libs').abspath)
else:
	myenv.Append(CPPPATH = ['#ThirdParty/MagicEyes/Include'])
	myenv.Append(LIBPATH = ['#ThirdParty/MagicEyes/Libs/' + vars['cpu_subdir']])
						
mymodule	= MakeMyModule(myenv, module_name, modsrcs, modlibs, kBuildModule, vars)
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI, vars)
Depends(mympi, mymodule)

#-----------------------------------------------------------------------------
# Modify the build environment and build and run the unit tests
#-----------------------------------------------------------------------------
RunMyTests(module_name, [], mpilibs + ['DebugMPI'], myenv, vars)

Return('mymodule', 'mympi')
