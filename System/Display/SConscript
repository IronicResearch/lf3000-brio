#-----------------------------------------------------------------------------
# Brio/System/Display
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *
import os

Import('env')

module_name	= 'Display'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
if env['cpu'] == 'x86':
	emulibs		=  ['X11']
	ogllibs		=  ['gles_cl']
	modsrcs		=  Glob('*.cpp') + Glob( os.path.join('Emulation', '*.cpp') )
elif env['platform'] == 'LF2000':
	emulibs		= []
	ogllibs		= ['EGL', 'GLESv1_CM', 'vr5']
	modsrcs		= Glob('*.cpp') + Glob( os.path.join('Lightning', '*.cpp') )
else:
	emulibs		= []
	ogllibs		= ['opengles_lite']
	modsrcs		= Glob('*.cpp') + Glob( os.path.join(env['platform'], '*.cpp') )

mpisrcs		= Glob( os.path.join('PublicMPI', '*.cpp') )
modlibs		= ['DebugMPI', 'KernelMPI'] + emulibs
mpilibs		= ['ModuleMPI'] + ogllibs					
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module
#-----------------------------------------------------------------------------
myenv		= env.Clone()
myenv.Append(CPPPATH=['Include'])
if env['cpu'] == 'x86':
	myenv.Append(CPPPATH = ['#ThirdParty/PowerVR/Include',
							'#ThirdParty/PowerVR/Include/LinuxPC',
							'/usr/X11R6/include'])
	myenv.Append(LIBPATH = ['#ThirdParty/PowerVR/Libs'])
elif env['platform'] == 'LF2000':
	myenv.Append(CPPPATH = ['#ThirdParty/Nexell/Include'])
	myenv.Append(LIBPATH = ['#ThirdParty/Nexell/Libs' + env['cpu']])
else:
	myenv.Append(CPPPATH = ['#ThirdParty/MagicEyes/Include'])
	myenv.Append(LIBPATH = ['#ThirdParty/MagicEyes/Libs/' + env['cpu']])
						
mymodule	= MakeMyModule(myenv, module_name, modsrcs, modlibs, kBuildModule)
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI)
Depends(mympi, mymodule)

targets = mympi or []
#-----------------------------------------------------------------------------
# Modify the build environment and build and run the unit tests
#-----------------------------------------------------------------------------
if env['cpu'] == 'x86':
	myenv.Append(RPATH   = myenv.Dir('#ThirdParty/PowerVR/Libs').abspath)

if env['buildtests']:
	targets.append(RunMyTests(module_name, [], mpilibs + ['DebugMPI', 'EventMPI'], myenv))

Return('mymodule', 'mympi')
