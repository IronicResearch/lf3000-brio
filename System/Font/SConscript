#-----------------------------------------------------------------------------
# Brio/System/Font
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *
Import('*')

module_name	= 'Font'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
modsrcs		= FindModuleSources(Dir('.'), vars)
mpisrcs		= FindMPISources(Dir('.'), vars)
modlibs		= ['DebugMPI', 'KernelMPI', 'freetype']
mpilibs		= ['ModuleMPI']
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module
#-----------------------------------------------------------------------------
myenv		= env.Clone()
myenv.Append(CPPPATH = ['#ThirdParty/FreeType/Include',
						'#ThirdParty/FreeType/Include/freetype'])

myenv.Append(LIBPATH = ['#ThirdParty/FreeType/Libs/' + vars['cpu_subdir']])
						
mymodule	= MakeMyModule(myenv, module_name, modsrcs, modlibs, kBuildModule, vars)
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI, vars)
Depends(mympi, mymodule)

#-----------------------------------------------------------------------------
# Modify the build environment and build and run the unit tests
#-----------------------------------------------------------------------------
hwlibs		= vars['variant'] == 'LF1000' and ['opengles_lite'] or ['ogl']
ogllibs		= vars['is_emulation'] and ['gles_cl'] or hwlibs
if vars['is_emulation']:
	myenv.Append(CPPPATH = ['#ThirdParty/PowerVR/Include',
							'#ThirdParty/PowerVR/Include/LinuxPC'])
	myenv.Append(LIBPATH = ['#ThirdParty/PowerVR/Libs'])
	myenv.Append(RPATH   = myenv.Dir('#ThirdParty/FreeType/Libs/x86').abspath)
	myenv.Append(RPATH   = myenv.Dir('#ThirdParty/PowerVR/Libs').abspath)
else:
	myenv.Append(CPPPATH = ['#ThirdParty/MagicEyes/Include'])
	myenv.Append(LIBPATH = ['#ThirdParty/MagicEyes/Libs/' + vars['cpu_subdir']])

if not vars['is_publish']:
	RunMyTests(module_name, [], mpilibs + ogllibs + ['DisplayMPI', 'EventMPI'], myenv, vars)

Return('mymodule', 'mympi')
