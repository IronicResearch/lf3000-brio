#-----------------------------------------------------------------------------
# Brio/System/Audio
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *

import os.path

Import('env')

module_name	= 'Audio'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
modsrcs		= Glob('*.cpp')
mpisrcs		= Glob(os.path.join('PublicMPI', '*.cpp'))
modlibs		= ['KernelMPI', 'DebugMPI', 'EventMPI', 
				'pthread', 'vorbisidec', 'ogg', 'sndfile',
				'Utility', 'avcodec', 'avformat', 'avutil', 'asound']
if env['cpu'] == 'x86':
	modlibs += ['portaudio']
	
mpilibs		= ['ModuleMPI', 'KernelMPI', 'DebugMPI']
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module

#-----------------------------------------------------------------------------
myenv		= env.Clone()

#myenv.Append(CPPDEFINES = ['USE_VORBIS'])
myenv.Append(CPPDEFINES = ['USE_44KHZ'])
myenv.Append(CPPPATH = ['#ThirdParty/Portaudio/Include'])
myenv.Append(CPPPATH = ['#ThirdParty/Theora/Include'])
myenv.Append(CPPPATH = ['#ThirdParty/LibSndFile/Include'])
myenv.Append(CPPPATH = ['#ThirdParty/LibAVFormat/Include'])
myenv.Append(LIBPATH = ['#ThirdParty/Portaudio/Libs/' + env['cpu']])
myenv.Append(LIBPATH = ['#ThirdParty/Theora/Libs/' + env['cpu']])
myenv.Append(LIBPATH = ['#ThirdParty/LibSndFile/Libs/' + env['cpu']])
myenv.Append(LIBPATH = ['#ThirdParty/LibAVFormat/Libs/' + env['cpu']])

myenv.Append(LIBPATH = [os.path.join(myenv['staging_dir'].abspath, 'usr', 'lib')])

myenv.Append(CPPPATH=['Include'])

mymodule	= MakeMyModule(myenv, module_name, modsrcs, modlibs, kBuildModule)
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI)
Depends(mympi, mymodule)

targets = mympi or []
 #-----------------------------------------------------------------------------
 # Modify the build environment and build and run the unit tests
 #-----------------------------------------------------------------------------
 #~ if not vars['is_publish']:
if env['cpu'] == 'x86':
	myenv.Append(RPATH   = [myenv.Dir('#ThirdParty/Portaudio/Libs/x86').abspath, myenv.Dir('#ThirdParty/Theora/Libs/x86').abspath])
if env['buildtests']:
	targets.append(RunMyTests(module_name, [], mpilibs + ['EventMPI', 'Utility', 'ButtonMPI'], myenv))

Return('targets')
