#-----------------------------------------------------------------------------
# Brio/System/Audio
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *

import os.path
Import('*')

module_name	= 'Audio'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
modsrcs		= FindModuleSources(Dir('.'), vars)
mpisrcs		= FindMPISources(Dir('.'), vars)
modlibs		= ['KernelMPI', 'DebugMPI', 'EventMPI', 'SystemResourceMPI', 
				'portaudio', 'me2000', 'vorbisidec', 'dsputil']
mpilibs		= ['ModuleMPI', 'KernelMPI', 'DebugMPI']
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module
#-----------------------------------------------------------------------------
myenv		= env.Copy()

myenv.Append(CPPPATH = ['#ThirdParty/Portaudio/Include'])
myenv.Append(CPPPATH = ['#ThirdParty/Mobileer/Include'])
myenv.Append(CPPPATH = ['#ThirdParty/Theora/Include'])
myenv.Append(CPPPATH = ['#System/Audio/DspUtil'])

if vars['is_emulation']:
	myenv.Append(RPATH   = myenv.Dir('#ThirdParty/Theora/Libs/x86').abspath)

myenv.Append(LIBPATH = ['#ThirdParty/Portaudio/Libs/' + vars['cpu_subdir']])
myenv.Append(LIBPATH = ['#ThirdParty/Mobileer/Libs/' + vars['cpu_subdir']])
myenv.Append(LIBPATH = ['#ThirdParty/Theora/Libs/' + vars['cpu_subdir']])
myenv.Append(LIBPATH = ['#System/Audio/DspUtil/' + vars['cpu_subdir']])

mymodule	= MakeMyModule(myenv, module_name, modsrcs, modlibs, kBuildModule, vars)
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI, vars)
Depends(mympi, mymodule)

#-----------------------------------------------------------------------------
# Modify the build environment and build and run the unit tests
#-----------------------------------------------------------------------------
RunMyTests(module_name, [], mpilibs + ['EventMPI', 'ResourceMPI'], myenv, vars)

Return('mymodule', 'mympi')
