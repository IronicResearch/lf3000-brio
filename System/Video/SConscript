#-----------------------------------------------------------------------------
# Brio/System/Video
#-----------------------------------------------------------------------------
from Etc.Tools.SConsTools.Priv.LfUtils import *
import os
Import('env')

module_name	= 'Video'
mpi_name	= module_name + 'MPI'

#-----------------------------------------------------------------------------
# Setup module source files and libraries
#-----------------------------------------------------------------------------
modsrcs		= Glob('*.cpp')
mpisrcs		= Glob(os.path.join('PublicMPI', '*.cpp'))
modlibs		= ['AudioMPI', 'DisplayMPI', 'EventMPI', 'KernelMPI', 
					 'DebugMPI', 'ogg', 'theora', 
					 'avformat', 'avcodec', 'avutil'] 
if (env['platform'] == 'LF2000' or env['platform'] == 'LF3000') and env['cpu_bare']:
	modlibs	+= ['gstreamer-0.10', 'glib-2.0', 'gobject-2.0', 'gstvideo-0.10']
mpilibs		= ['ModuleMPI']
						
#-----------------------------------------------------------------------------
# Modify the build environment and build the module
#-----------------------------------------------------------------------------
myenv		= env.Clone()
myenv.Append(CPPPATH = ['#System/Display/Include'])
myenv.Append(CPPPATH = ['#ThirdParty/Theora/Include'])
myenv.Append(LIBPATH = ['#ThirdParty/Theora/Libs/' + env['cpu']])

nfsroot		= os.getenv('OECORE_TARGET_SYSROOT')
if nfsroot == None:
	nfsroot = env['staging_dir'].abspath
if env['cpu'] == 'x86':
	nfsroot = '/'
myenv.Append(CPPPATH = [os.path.join(nfsroot, 'usr', 'include', 'glib-2.0')])
myenv.Append(CPPPATH = [os.path.join(nfsroot, 'usr', 'include', 'gstreamer-0.10')])
myenv.Append(CPPPATH = [os.path.join(nfsroot, 'usr', 'include', 'libxml2')])
myenv.Append(LIBPATH = [os.path.join(nfsroot, 'usr', 'lib')])
if (env['platform'] == 'LF2000' or env['platform'] == 'LF3000') and env['cpu_bare']:
	myenv.Append(CPPDEFINES = ['USE_GSTREAMER=1'])
	myenv.Append(CPPDEFINES = ['HAVE_CONFIG_H', 'G_THREADS_MANDATORY', 'G_DISABLE_CAST_CHECKS', 'G_DISABLE_ASSERT'])

myenv.Append(CPPPATH = ['#ThirdParty/LibAVFormat/Include'])
myenv.Append(LIBPATH = ['#ThirdParty/LibAVFormat/Libs/' + env['cpu']])

#OpenGL emulation
if env['cpu'] == 'x86':
	myenv.Append(CPPPATH = ['#ThirdParty/MaliEmulator/include'])
	myenv.Append(LIBPATH = ['#ThirdParty/MaliEmulator/bin'])

if env['platform'] == 'LF2000' and env['cpu_bare']:
	myenv.Append(CPPPATH = ['#ThirdParty/Nexell/Include'])
	myenv.Append(CPPDEFINES = ['USE_ROTATOR=1'])
						
elif env['platform'] == 'Lightning' and env['cpu_bare']:
	myenv.Append(CPPPATH = ['#ThirdParty/MagicEyes/Include'])

myenv.Append(CPPPATH=['Include'])
mymodule	= MakeMyModule(myenv, module_name, modsrcs, modlibs, kBuildModule)
mympi		= MakeMyModule(myenv, mpi_name, mpisrcs, mpilibs, kBuildMPI)
Depends(mympi, mymodule)

targets = mympi or []
#-----------------------------------------------------------------------------
# Modify the build environment and build and run the unit tests
#-----------------------------------------------------------------------------
if env['cpu'] == 'x86':
	myenv.Append(RPATH   = [myenv.Dir('#ThirdParty/Theora/Libs/x86').abspath, myenv.Dir('#ThirdParty/Portaudio/Libs/x86').abspath])
	myenv.Append(RPATH   = [myenv.Dir('#ThirdParty/LibAVFormat/Libs/x86').abspath])

if env['buildtests']:
	if env['cpu'] != 'x86':
		targets.append(RunMyTests(module_name, [], mpilibs +  ['DisplayMPI', 'EventMPI', 'AudioMPI', 'KernelMPI'], myenv))
	else:
		print "WARNING: Video test disabled in emulation until fixed!"

Return('targets')
